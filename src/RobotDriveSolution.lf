target C {
    platform: {
        name: "rp2040",
        board: "pololu_3pi_2040_robot"
    },
    single-threaded: true
}

import Display from "lib/Display.lf"
import Motors from "lib/Motors.lf"

reactor Robot {
    input drive: bool
    output notify: string
    output left_power: float
    output right_power: float

    reaction(startup) -> notify, left_power, right_power {=
        lf_set(notify, "INIT");
        // ensure motors are stopped at startup
        lf_set(left_power, 0.0f);
        lf_set(right_power, 0.0f);
    =}

    initial mode STOPPED {
        reaction(drive) -> reset(DRIVING), notify, left_power, right_power {=
        if (drive->value) {
            lf_set_mode(DRIVING);
            lf_set(notify, "DRIVING");
            // set motors to move forward
            lf_set(left_power, 0.1f);
            lf_set(right_power, 0.1f);
        }
        =}
    }

    mode DRIVING {
        reaction(drive) -> reset(STOPPED), notify, left_power, right_power {=
        if (!drive->value) {
            lf_set_mode(STOPPED);
            lf_set(notify, "STOPPED");
            // set motors to stop
            lf_set(left_power, 0.0f);
            lf_set(right_power, 0.0f);
        }
        =}
    }
}

main reactor {
    timer t(0, 2 sec)
    state drive: bool = false
    robot = new Robot()
    display = new Display()
    motors = new Motors()

    robot.notify -> display.line0
    robot.left_power -> motors.left_power
    robot.right_power -> motors.right_power

    reaction(t) -> robot.drive {=
        lf_set(robot.drive, self->drive);
        self->drive = !self->drive;
    =}
}