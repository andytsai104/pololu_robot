target C {
    platform: {
        name: "rp2040",
        board: "pololu_3pi_2040_robot"
    },
    single-threaded: true,
    keepalive: true
}

main reactor {
    preamble {=
        #include <stdio.h>
        #include "pico/stdlib.h"
        #include "hardware/gpio.h"

        // GPIO pin number for Button A
        # define BUTTON_A_PIN 25

        // pointer for the physical action
        static void* action_ptr = NULL;

        // defining ISR
        void gpio_callback(uint gpio, uint32_t event_mask) {
            // printing ISR arguments
            printf("Interrupt callback: gpio=%u, mask=0x%08x\n", gpio, event_mask);

            // schedule a physical action
            if (action_ptr != NULL) {
                lf_schedule(action_ptr, 0);
            }
        }
    =}

    // defining a physical action triggered by the ISR
    physical action button_press;

    reaction(startup) -> button_press {=
        stdio_init_all();

        // setting up the button GPIO
        gpio_init(BUTTON_A_PIN);
        gpio_set_dir(BUTTON_A_PIN, GPIO_IN);
        gpio_pull_up(BUTTON_A_PIN);

        // clearing any prior interrupt state
        gpio_acknowledge_irq(BUTTON_A_PIN,
                                GPIO_IRQ_EDGE_RISE | GPIO_IRQ_EDGE_FALL |
                                GPIO_IRQ_LEVEL_HIGH | GPIO_IRQ_LEVEL_LOW);

        // registering ISR on rising edge
        gpio_set_irq_enabled_with_callback(
            BUTTON_A_PIN,
            GPIO_IRQ_EDGE_RISE,
            true,
            gpio_callback
        );

        // storing pointer for ISR scheduling
        action_ptr = button_press;
        printf("Interrupt configured for GPIO %u (rising edge)\n", BUTTON_A_PIN);
    =}

    // button press reaction invoked when ISR schedules the action
    reaction(button_press) {=
        printf("Button press detected â€” logical time %lld\n",
        (long long)lf_time_logical());
    =}
}
