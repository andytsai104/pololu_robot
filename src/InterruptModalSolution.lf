target C {
    platform: {
    name: "rp2040",
    board: "pololu_3pi_2040_robot"
    },
    single-threaded: true,
    keepalive: true
}

import Display from "lib/Display.lf"

reactor ModalCounter {
    preamble {=
        #include <stdio.h>
        #include "pico/stdlib.h"
        #include "hardware/gpio.h"
        
        // GPIO pin number for Button A
        #define BUTTON_A_PIN 25
        
        // pointer for the physical action
        static void* action_ptr = NULL;
        
        // defining ISR
        void gpio_callback(uint gpio, uint32_t event_mask) {
        
        // schedule a physical action
        if (action_ptr != NULL) {
            lf_schedule(action_ptr, 0);
        }
        }
    =}

    d = new Display()

    // defining a physical action triggered by the ISR
    physical action button_press

    // global state for the counter
    state count: int = 0

    // timers for counting
    timer t1(0, 500 msec)       // up count timer
    timer t2(0, 1000 msec)      // down count timer

    reaction(startup) -> button_press {=
    stdio_init_all();
    
    // setting up the button GPIO
    gpio_init(BUTTON_A_PIN);
    gpio_set_dir(BUTTON_A_PIN, GPIO_IN);
    gpio_pull_up(BUTTON_A_PIN);
    
    // clearing any prior interrupt state
    gpio_acknowledge_irq(BUTTON_A_PIN,
                            GPIO_IRQ_EDGE_RISE | GPIO_IRQ_EDGE_FALL |
                            GPIO_IRQ_LEVEL_HIGH | GPIO_IRQ_LEVEL_LOW);
    
    // registering ISR on rising edge
    gpio_set_irq_enabled_with_callback(
        BUTTON_A_PIN,
        GPIO_IRQ_EDGE_RISE,
        true,
        gpio_callback
    );
    
    // storing pointer for ISR scheduling
    action_ptr = button_press;
    =}

    initial mode CountUp {
    reaction(t1) -> d.line0 {=
        static char buf0[17];
        snprintf(buf0, 17, "Up Count: %d", self->count);
        lf_set(d.line0, buf0);
        self->count++;
    =}
    
    // button press reaction invoked when ISR schedules the action
    reaction(button_press) -> CountDown {=
        (long long)lf_time_logical();
        // Transition to CountDown mode when button A is pressed
        lf_set_mode(CountDown);
    =}
    }

    mode CountDown {
    reaction(t2) -> d.line0 {=
        static char buf0[17];
        snprintf(buf0, 17, "Down Count: %d", self->count);
        lf_set(d.line0, buf0);
        self->count--;
    =}
    
    // button press reaction invoked when ISR schedules the action
    reaction(button_press) -> CountUp {=
        (long long)lf_time_logical();
        // Transition back to CountUp mode when button A is pressed
        lf_set_mode(CountUp);
    =}
    }
}

main reactor {
    counter = new ModalCounter()
}